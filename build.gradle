plugins {
    id "java"
    id "idea"
    id "maven"
    id "maven-publish"
    id "jacoco"
    id "com.jfrog.bintray" version "1.8.2"
}

String projectName = 'Commandler-Core'
String projectVersion = '2.0.0'
String projectDescription = "Commandler is a generic command handling framework ready to be implemented with any platform."
String projectGit = 'https://gitlab.com/Elypia/Commandler.git'

jar {
    from sourceSets.main.allSource

    manifest {
        attributes 'Implementation-Title'   : projectName,
                   'Implementation-Version' : projectVersion
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir

    javadoc {
        options {
            links += [
                'https://docs.oracle.com/javase/8/docs/api/'
            ]
        }
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Commandler']
    publish = true

    pkg {
        repo = projectName
        name = projectName
        userOrg = 'elypia'
        licenses = ['Apache-2.0']
        vcsUrl = projectGit

        version {
            name = projectVersion
            desc = projectDescription
            released = new Date()
        }
    }
}

publishing {
    def details = {
        licenses{
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution projectName
            }
        }

        developers{
            developer {
                id 'Seth'
                name 'Seth'
                email "seth@elypia.com"
            }
        }

        scm{
            url projectGit
        }
    }

    publications {
        Commandler(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'com.elypia'
            artifactId 'commandler'
            version projectVersion

            pom.withXml {
                def root = asNode()

                root.appendNode('description', projectDescription)
                root.appendNode('name', projectName)
                root.appendNode('url', 'https://elypia.com/')
                root.children().last() + details
            }
        }
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'jacoco'

    version = projectVersion

    repositories {
        jcenter()
    }

    dependencies {
        implementation group: 'org.apache.velocity', name: 'velocity-engine-core', version: '2.0'
        implementation 'org.jsoup:jsoup:1.11.3'
        implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.2.0'

        testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.2.0'
        testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-runner', version: '1.2.0'
        testRuntimeOnly group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.11.0'
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport

    reports {
        html.enabled = false
    }
}

jacoco {
    toolVersion = "0.8.1"
}

task wrapper(type: Wrapper) {
    description = 'Generates gradlew[.bat] scripts'
    gradleVersion = '4.8.1'
}

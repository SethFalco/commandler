plugins {
    id "com.jfrog.bintray" version "1.8.4" apply false
}

allprojects() {
    apply plugin: "idea"
    apply plugin: "jacoco"

    version = "3.0.0"
    description = "Commandler is a abstract command handling framework ready to be implemented with any platform."

    ext {
        projectGit = 'https://gitlab.com/Elypia/commandler.git'
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    repositories {
        mavenCentral()
        jcenter()
    }
}

subprojects { subproject ->
    apply plugin: "java-library"
    apply plugin: "maven-publish"
    apply plugin: "com.jfrog.bintray"

    jar {
        from sourceSets.main.allSource

        manifest {
            attributes  "Implementation-Title": subproject.name,
                        "Implementation-Version": version
        }
    }

    javadoc {
        options {
            options.encoding = 'UTF-8'
            options.addStringOption('-html5')

            links += [
                "https://docs.oracle.com/javase/10/docs/api/"
            ]
        }
    }

    bintray {
        user = System.getenv("BINTRAY_USER")
        key = System.getenv("BINTRAY_KEY")
        publications = ["Commandler"]
        publish = true

        pkg {
            repo = "maven"
            name = subproject.name
            userOrg = "elypia"
            licenses = ['Apache-2.0']
            vcsUrl = projectGit
            publicDownloadNumbers = true

            version {
                name = version
                desc = description
                released = new Date()
            }
        }
    }

    publishing {
        publications {
            Commandler(MavenPublication) {
                from components.java
                groupId 'com.elypia.commandler'
                artifactId subproject.name
                version version
                artifact javadoc
                artifact classes
            }
        }
    }

    dependencies {
        // Logging
        implementation "org.slf4j:slf4j-api:1.7.26"
        testRuntimeOnly "ch.qos.logback:logback-classic:1.2.3"

        // Testing
        testImplementation "org.junit.jupiter:junit-jupiter:5.4.2"
    }

    /**
     * Testing and Code Coverage
     */

    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport

        reports {
            html.enabled = false
        }
    }

    jacoco {
        toolVersion = "0.8.4"
    }

    jacocoTestReport {
        reports {
            html.enabled = true
            xml.enabled = false
        }
    }
}
